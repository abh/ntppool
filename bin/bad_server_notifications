# -*- cperl -*-
eval 'exec $CBROOT/bin/perl $0 ${1+"$@"}'
  unless $running_under_some_shell;
use strict;
use warnings;
use lib "$ENV{CBROOTLOCAL}/lib";
use lib "$ENV{CBROOT}/lib";
use Combust::Config;
use Combust::Template;

use Email::Stuffer;
use NP::Email;
use NP::Model;

my $config = Combust::Config->new;
my $deployment_mode = $config->site->{ntppool}->{deployment_mode};

my $dbh = NP::Model->dbh;

$dbh->selectrow_array(q[SELECT GET_LOCK("notifications", 0)]) or exit;

$dbh->do(
    q[delete from sa using server_alerts sa inner join servers s on (s.id=sa.server_id) where s.score_raw > 4]
);

my $admins = NP::Model->user->admins_to_notify;

my $tpl = Combust::Template->new;

my $i = 0;

for my $admin (@$admins) {
    my $param = {admin => $admin};
    my @bad_servers = $admin->bad_servers;
    $param->{bad_servers} = \@bad_servers;
    my $msg = $tpl->process('tpl/bad_server_notification.txt',
        $param, {site => 'ntppool', config => $config});

    my $email = Email::Stuffer
      ->from(NP::Email::address("sender"))
      ->to($admin->email)
      ->reply_to(NP::Email::address("support"))
      ->subject("NTP Pool: Problems with your NTP server (" . $bad_servers[0]->ip . ")")
      ->text_body($msg);

    NP::Email::sendmail($email->email);

    for my $server (@bad_servers) {
        my $alert = $server->alert;
        $alert->mark_sent;
    }

    #last;
    if ($deployment_mode ne "prod") {
        last if ++$i == 1;
    }
}

print "done\n";

1;
